plugins {
	id 'com.gradle.build-scan' version '1.13.4'
	id 'com.gradle.plugin-publish' version '0.10.1'
	id 'java-gradle-plugin'
	id 'maven-publish'
	id 'idea'
}

repositories {
	mavenCentral()
	jcenter()
	maven {
		name 'jboss-nexus'
		url "https://repository.jboss.org/nexus/content/groups/public/"
	}
}

gradlePlugin {
	plugins {
		provisionPlugin {
			id = 'org.wildfly.build.provision'
			implementationClass = 'org.wildfly.build.gradle.provisioning.ProvisionPlugin'
		}
		featurepackBuildPlugin {
			id = 'org.wildfly.build.featurepack'
			implementationClass = 'org.wildfly.build.gradle.featurepackbuild.FeaturePackBuilderPlugin'
		}
	}
}

publishing {
	repositories {
		maven {
			url "~/.m2/repository"
		}
	}
}

dependencies {
	compile 'org.wildfly.build:wildfly-server-provisioning:1.2.11.Final'
	compile gradleApi()
	testCompile 'junit:junit:4.12'
	testCompile gradleTestKit()
}

idea {
	project {
		languageLevel = '1.8'
	}
	module {
		downloadSources = true
		downloadJavadoc = true
	}
}

buildScan {
	//See also: https://docs.gradle.com/build-scan-plugin/
	termsOfServiceUrl = 'https://gradle.com/terms-of-service';
	termsOfServiceAgree = 'yes'
}

pluginBundle {
	website = 'https://github.com/wildfly/wildfly-gradle-tools'
	vcsUrl = 'https://github.com/wildfly/wildfly-gradle-tools.git'
	description = 'A gradle plugin to materialize a custom WildFly instance from a server provisioning definition'
	tags = ['java', 'jboss', 'module', 'gradle', 'plugin', 'wildfly']

	plugins {
		provisionPlugin {
			id = 'org.wildfly.build.provision'
			displayName = 'WildFly Provision Plugin'
			description = 'A gradle plugin to materialize a custom WildFly instance from a server provisioning definition'
		}
		featurepackPlugin {
			id = 'org.wildfly.build.featurepack'
			displayName = 'WildFly Feature Pack builder'
			description = 'A gradle plugin to help creating a custom WildFly Feature Pack'
		}
	}
}

wrapper {
	distributionType = Wrapper.DistributionType.ALL
}

description = 'A WildFly Provisioning Plugin for Gradle users'
version = '0.0.9-SNAPSHOT'
group 'org.wildfly.build'
